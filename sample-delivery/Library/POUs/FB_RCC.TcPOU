<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_RCC" Id="{47a0acf2-060b-4e55-9edc-089d8f8e62bc}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_RCC
VAR_IN_OUT
	stRCC_VRC_01 : ST_VRC;
	stRCC_VRC_02 : ST_VRC;
	stRCC_VRC_03 : ST_VRC;
	stRCC_VRC_04 : ST_VRC;
	stRCC_GCP_01 : ST_VG;
END_VAR
VAR
	{attribute 'pytmc' :=' pv: ILK_Setpoint '}
    RCC_GCP_01_INTERLOCK_SETPOINT : REAL := 10;
	{attribute 'pytmc' :=' pv: OverrideMode '}
	xSystemOverrideMode_RCC : BOOL := FALSE;
	{attribute 'pytmc' :=' pv: sLogMessage '}
	sLogMessage : T_MaxString := '';
	
	{attribute 'pytmc' :=' pv: T1 '}
	T1 : TIME := T#20S;
	{attribute 'pytmc' :=' pv: T2 '}
	T2 : TIME := T#10S;
	{attribute 'pytmc' :=' pv: T3 '}
	T3 : TIME := T#5S;
	{attribute 'pytmc' :=' pv: T4 '}
	T4 : TIME := T#10S;
	{attribute 'pytmc' :=' pv: RUNCLEAN'}
	RUN_CLEAN : BOOL := TRUE;
	
	{attribute 'pytmc' :=' pv: CLEAR_ERROR'}
	bClearError : BOOL := FALSE;
	
	{attribute 'pytmc' :=' pv: ERROR'}
	bError : BOOL := FALSE;
	
	fbFormat   : FB_FormatString;
	
	tTimer1 : TON;
	current_state : INT := E_RCC_STATES.IDLE;
	init_cycle : BOOL  := FALSE;
	
	RCC_VRC_01_OPEN_OK : BOOL;
    RCC_VRC_02_OPEN_OK : BOOL;
    RCC_VRC_03_OPEN_OK : BOOL;
    RCC_VRC_04_OPEN_OK : BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
(* Valve can open only when CRIX-RCC-VRC-02 and 03 are closed*)
RCC_VRC_01_OPEN_OK := ( stRCC_VRC_02.i_xClsLS AND NOT stRCC_VRC_02.q_xOPN_DO ) AND
                                    ( stRCC_VRC_03.i_xClsLS AND NOT stRCC_VRC_03.q_xOPN_DO ) ;

(* Valve can open only when CRIX-RCC-VRC-01 and 04 are closed*)
RCC_VRC_02_OPEN_OK := ( stRCC_VRC_01.i_xClsLS AND NOT stRCC_VRC_01.q_xOPN_DO ) AND
                                    ( stRCC_VRC_04.i_xClsLS AND NOT stRCC_VRC_04.q_xOPN_DO );

(* Valve can open only when CRIX-RCC-GCP-01 reading < setpoint Torr and CRIX-RCC-VRC-01 and 04 are closed*)
RCC_VRC_03_OPEN_OK := (stRCC_GCP_01.rPRESS < RCC_GCP_01_INTERLOCK_SETPOINT AND stRCC_GCP_01.xPRESS_OK ) AND
                                 ( stRCC_VRC_01.i_xClsLS AND NOT stRCC_VRC_01.q_xOPN_DO ) AND
                                              ( stRCC_VRC_04.i_xClsLS AND NOT stRCC_VRC_04.q_xOPN_DO );


(* Valve can open only when CRIX-RCC-VRC-02 and 03 are closed*)
RCC_VRC_04_OPEN_OK := ( stRCC_VRC_02.i_xClsLS AND NOT stRCC_VRC_02.q_xOPN_DO ) AND
                                      ( stRCC_VRC_03.i_xClsLS AND NOT stRCC_VRC_03.q_xOPN_DO ) ;
			
 CASE current_state OF
	E_RCC_STATES.ERROR: // SOMETHING WENT WRONG
		stRCC_VRC_01.q_xOPN_DO := FALSE;
		stRCC_VRC_02.q_xOPN_DO := FALSE;
		stRCC_VRC_03.q_xOPN_DO := FALSE;
		stRCC_VRC_04.q_xOPN_DO := FALSE; 
		
		IF bClearError THEN 
			current_state := E_RCC_STATES.IDLE; 
			bError := FALSE;
		END_IF

	E_RCC_STATES.IDLE: // -1
		IF init_cycle THEN 
			current_state := E_RCC_STATES.INIT; 
			init_cycle := FALSE;
		END_IF
		
	E_RCC_STATES.INIT: // 0
		stRCC_VRC_01.q_xOPN_DO := FALSE;
		stRCC_VRC_02.q_xOPN_DO := FALSE;
		stRCC_VRC_03.q_xOPN_DO := FALSE;
		stRCC_VRC_04.q_xOPN_DO := FALSE;
		current_state:= E_RCC_STATES.OPEN_VRC_02_FOR_T1;
		
	E_RCC_STATES.OPEN_VRC_02_FOR_T1: // OPEN 1
		tTimer1(IN:=TRUE, PT:=T1);
		stRCC_VRC_02.q_xOPN_DO := TRUE;
	
		IF tTimer1.Q THEN 
			current_state := E_RCC_STATES.DRAIN;
			tTimer1(IN:=FALSE); // RESTART TIMER
		END_IF
		
	 E_RCC_STATES.DRAIN:	// DRAIN_1 2
		// THIS MIGHT CAUSE THE RCC TO STALL IF THIS CONDITION IS NEVER MET
		
		IF (NOT stRCC_GCP_01.xPRESS_OK) THEN 
			current_state := E_RCC_STATES.ERROR; 
			bError := TRUE;
		END_IF
		
		IF RCC_VRC_03_OPEN_OK THEN
			IF run_clean THEN stRCC_VRC_02.q_xOPN_DO := FALSE; END_IF
			stRCC_VRC_03.q_xOPN_DO := TRUE;
			current_state := E_RCC_STATES.DRAIN_CHECK; 
		END_IF
		
	E_RCC_STATES.DRAIN_CHECK:	// CHECK IF WE ARE DON DRAINING 3
		tTimer1(IN:=TRUE, PT:=T2);
		
		IF tTimer1.Q THEN 
			current_state := E_RCC_STATES.OPEN_VRC_01_FOR_T3;
			IF NOT RCC_VRC_03_OPEN_OK THEN current_state := E_RCC_STATES.OPEN_VRC_02_FOR_T1; END_IF
 			stRCC_VRC_03.q_xOPN_DO := FALSE;
			IF NOT run_clean THEN stRCC_VRC_02.q_xOPN_DO := FALSE; END_IF
			tTimer1(IN:=FALSE); // RESTART TIMER
		END_IF
		
	 E_RCC_STATES.OPEN_VRC_01_FOR_T3: // OPEN VRC_01 for T3 4
		tTimer1(IN:=TRUE, PT:=T3);
		stRCC_VRC_01.q_xOPN_DO := TRUE;
		IF tTimer1.Q THEN 
			current_state := E_RCC_STATES.OPEN_VRC_04_FOR_T4;
			tTimer1(IN:=FALSE); // RESTART TIMER
		END_IF
		
	 E_RCC_STATES.OPEN_VRC_04_FOR_T4: // OPEN VRC_04 for T4 5
		tTimer1(IN:=TRUE, PT:=T4);
		stRCC_VRC_04.q_xOPN_DO := TRUE;
		IF tTimer1.Q THEN 
			current_state := E_RCC_STATES.IDLE;
			stRCC_VRC_01.q_xOPN_DO := FALSE;
			stRCC_VRC_04.q_xOPN_DO := FALSE;
			tTimer1(IN:=FALSE); // RESTART TIMER
		END_IF	
	
	
END_CASE
]]></ST>
    </Implementation>
    <Method Name="start_cycle" Id="{0638d37b-98e4-4dcd-b1fe-fea9c7d7701d}">
      <Declaration><![CDATA[METHOD start_cycle : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[start_cycle := init_cycle := TRUE;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_RCC">
      <LineId Id="3" Count="3" />
      <LineId Id="11" Count="3" />
      <LineId Id="19" Count="4" />
      <LineId Id="27" Count="4" />
      <LineId Id="36" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="208" Count="0" />
      <LineId Id="216" Count="2" />
      <LineId Id="215" Count="0" />
      <LineId Id="222" Count="1" />
      <LineId Id="231" Count="0" />
      <LineId Id="233" Count="0" />
      <LineId Id="232" Count="0" />
      <LineId Id="210" Count="0" />
      <LineId Id="73" Count="22" />
      <LineId Id="168" Count="0" />
      <LineId Id="212" Count="1" />
      <LineId Id="228" Count="0" />
      <LineId Id="230" Count="0" />
      <LineId Id="229" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="96" Count="36" />
      <LineId Id="37" Count="0" />
    </LineIds>
    <LineIds Name="FB_RCC.start_cycle">
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>